[%
// Parse model and transform it to model-independent Tuples (TODO: Use Emfatic)

operation t_scxml getModel() : Tuple {
  var result = new Tuple();
  result.name = self.a_name;
  
  var region = new Tuple();
  result.region = region;
  region.initial_state_id = self.a_initial;
  region.initial_history = false;
  region.states = new List();  

  for (state in self.c_state) {
    region.states.add(state.getModel());
  }
  
  return result;
}

operation t_state getModel() : Tuple {
  var result = new Tuple();
  result.name = self.a_id;
  result.id = self.a_id;
  result.region = null;
  result.parent_state_id = null;
  result.entry = null;
  result.exit = null;
  result.transitions_for_trigger = new Map();

  if (self.e_onentry.isDefined() and self.e_onentry.e_script.isDefined()) {
  	result.entry = self.e_onentry.e_script.a_src;
  }

  if (self.e_onexit.isDefined() and self.e_onexit.e_script.isDefined()) {
  	result.exit = self.e_onexit.e_script.a_src;
  }
  
  if (not self.c_state.isEmpty()) {
    var region = new Tuple();  
    result.region = region;

    if (self.e_initial.isDefined() and self.e_initial.e_transition.isDefined()) {
      region.initial_state_id = self.e_initial.e_transition.a_target;
      region.initial_history = false;
    }
    if (self.e_history.isDefined() and self.e_history.e_transition.isDefined()) {
      region.initial_state_id = self.e_history.e_transition.a_target;
      region.initial_history = true;
    }
    
    region.states = new List();
    for (state in self.c_state) {
      var state_model = state.getModel();
      state_model.parent_state_id = result.id;
      region.states.add(state_model);
    }
  }

  for (scxml_transition in self.c_transition) {
    var transition = scxml_transition.getModel(result);

    if (not result.transitions_for_trigger.containsKey(transition.trigger)) {
      result.transitions_for_trigger.put(transition.trigger, new List());
    }
    result.transitions_for_trigger.get(transition.trigger).add(transition);
  }

  return result;
}

operation t_transition getModel(state: Tuple) : Tuple {
  var result = new Tuple();
  result.actions = new List();
  result.guards = new List();
  result.all_transitions = new List();
  result.target_state_id = self.a_target;
  result.start = state.name;
  result.internal = (self.a_type = "internal");
  result.trigger = self.a_event;

  for (script in self.c_script) {
    result.actions.add(script.a_src);
  }

  if (self.a_cond.isDefined()) {
    result.guards.add(self.a_cond);
  }
  
  return result;
}
%]		
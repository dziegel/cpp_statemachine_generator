[%
operation Tuple transformStatemachine(reportErrors : Boolean) {
  self.transition_type = "Transition";
  self.transition_action_type = "ActionType";
  self.state_type = "State";
  self.state_entry_exit_type = "State::EntryExitType";
  self.history_state_type = "HistoryState";
  self.event_type = "Event";
  self.impl_type = "Impl";
  self.impl_ptr_type = "ImplPtr";
  
  self.all_states = new Map();
  self.all_actions_entry_exit = new List();
  self.all_guards = new List();
  
  self.region.regionCollectStates(self, null);
  self.region.regionTransform(self, null, reportErrors);
  
  self.all_actions_entry_exit = self.all_actions_entry_exit.sortBy(a | a);
  self.all_guards = self.all_guards.sortBy(g | g);
}

operation String cleanString() : String {
  var val = self.replace(" ", "");
  val = val.replace("\n", "");
  val = val.replace("\r", "");
  val = val.replace(";", ",");
  return val;
}

operation List splitEntries() : List {
  var result = new List();
  for (entry in self) {
    entry = entry.cleanString();
    if (entry.matches(".*,.*")) {
      result.addAll(entry.split(","));
    } else {
      result.add(entry);
    }
  }
  return result;
}

operation String splitEntries() : List {
  return self.cleanString().split(",");
}

operation Tuple regionCollectStates(statemachine : Tuple, parent_state : Tuple) {
  self.states = self.states.sortBy(s | s.name);
  
  for (state in self.states) {
    statemachine.all_states.put(state.id, state);

    // State variable name
    if (parent_state != null) {
      state.scoped_name = parent_state.scoped_name + "_" + state.name;
      state.readable_scoped_name = parent_state.readable_scoped_name + "::" + state.name;
    } else {
      state.scoped_name = state.name;
      state.readable_scoped_name = state.name;
    }
    state.variable_name = "k" + state.scoped_name;

    if (state.region != null) {
      state.region.regionCollectStates(statemachine, state);
    }
  }
}

operation Tuple regionTransform(statemachine : Tuple, parent_state : Tuple, reportErrors : Boolean) {
  // Transform states
  for (state in self.states) {
    state.stateTransform(statemachine, self, parent_state, reportErrors);
  }

  // Handle history state type
  if(self.initial_history) {
    parent_state.state_type = statemachine.history_state_type;
  }

  // Initial variable name
  var initial = statemachine.all_states.get(self.initial_state_id);
  if (reportErrors and (initial == null)) {
    ("Unable to find state id " + self.initial_state_id).errln();
  }
  self.initial_variable_name = initial.variable_name;
}

operation Tuple stateTransform(statemachine : Tuple, region : Tuple, parent_state : Tuple, reportErrors : Boolean) {
  self.transitions_for_trigger_transformed = new Map();
  if (reportErrors and (self.region == null) and self.transitions_for_trigger.isEmpty()) {
    (statemachine.name + ": No outgoing transitions from state " + self.name).errln();
  }

  // State type
  self.state_type = statemachine.state_type;

  // Parent state
  self.parent_variable_pointer = "nullptr";
  if (self.parent_state_id.isDefined()) {
    var parent = statemachine.all_states.get(self.parent_state_id);
    self.parent_variable_pointer = "&" + statemachine.name + "::" + parent.variable_name;
  }
  
  // Entry action
  self.entry_var = "{}";
  if (self.entry.isDefined()) {
    self.entry_var = statemachine.name + "_" + self.scoped_name + "_entry";

    if (reportErrors and (self.entry == "")) {
      (sm.name + ": Empty entry function in state " + self.name).errln();
    }

    var entry_pointers_list = new List();
    for (entry in self.entry.splitEntries()) {
      entry_pointers_list.add("&" + statemachine.name + "::" + "Impl::" + entry);
      if (not statemachine.all_actions_entry_exit.includes(entry)) {
        statemachine.all_actions_entry_exit.add(entry);
      }
    }
    self.entry_pointers = entry_pointers_list.concat(", ");
  }

  // Exit action
  self.exit_var = "{}";
  if (self.exit.isDefined()) {
    self.exit_var = statemachine.name + "_" + self.scoped_name + "_exit";

    if (reportErrors and (self.exit == "")) {
      (sm.name + ": Empty exit function in state " + self.name).errln();
    }

    var exit_pointers_list = new List();
    for (ex in self.exit.splitEntries()) {
      exit_pointers_list.add("&" + statemachine.name + "::" + "Impl::" + ex);
      if (not statemachine.all_actions_entry_exit.includes(ex)) {
        statemachine.all_actions_entry_exit.add(ex);
      }
    }
    self.exit_pointers = exit_pointers_list.concat(", ");
  }

  // Initial state
  self.initial_variable_pointer = "nullptr";
  if (self.region != null) {
    // Transform subregion of state
    self.region.regionTransform(statemachine, self, reportErrors);

    // Initial state
    self.initial_variable_pointer = "&" + statemachine.name + "::" + self.region.initial_variable_name;
  }

  // Collect all events
  self.all_events = new List();
  for (trigger in self.transitions_for_trigger.keySet()) {
    if (not self.all_events.includes(trigger)) {
      self.all_events.add(trigger);
    }
  }
  self.all_events = self.all_events.sortBy(e | e);

  // Transform transitions of state
  for (trigger in self.transitions_for_trigger.keySet()) {
    var transitions = new Tuple();
    transitions.trans_conditional = new List();
    transitions.trans_unconditional = new List();

    for (transition in self.transitions_for_trigger.get(trigger)) {
      transition.transitionTransform(statemachine, self, trigger, new List(), reportErrors);
      
      if (not transition.guards.isEmpty()) {
        transitions.trans_conditional.add(transition);
      } else {
        transitions.trans_unconditional.add(transition);
      }
    }
    if (reportErrors and (transitions.trans_unconditional.size() > 1)) {
      ("More than one unconditional outgoing transition from state " + self.name + " for trigger " + trigger).errln();
    }
    
    self.transitions_for_trigger_transformed.put(trigger, transitions);
  }
}

operation Tuple transitionTransform(statemachine : Tuple, state : Tuple, trigger: String, all_actions: List, reportErrors : Boolean) {
  self.trans_conditional = new List();
  self.trans_unconditional = new List();
  self.guards_list = "";
  self.target_variable_name = null;
  self.actions_list = null;

  // Collect all actions
  var actions = new List();
  for (action in self.actions.splitEntries()) {
    all_actions.add("&" + statemachine.name + "::" + statemachine.impl_type + "::" + action);
    if (not statemachine.all_actions_entry_exit.includes(action)) {
      if (reportErrors and (action == "")) {
        (sm.name + ": Empty function behavior in transition starting from state " + self.start).errln();
      }
      statemachine.all_actions_entry_exit.add(action);
    }
  }
  
  // Collect all guards and create guards_list
  var guards = new List();
  for (guard in self.guards.splitEntries()) {
    if (reportErrors and (guard == "")) {
      (sm.name + ": Empty guard in transition starting from state " + self.start).errln();
    }
    guards.add("impl->" + guard + "(event)");
    if (not statemachine.all_guards.includes(guard)) {
      statemachine.all_guards.add(guard);
    }
  }
  self.guards_list = guards.concat(" && ");
  
  if (self.internal) {
    if (not all_actions.isEmpty()) {
      self.actions_list = all_actions.concat(", ");
    }
  }

  // Transition has a target state
  if(not self.internal and (self.target_state_id != null)) {
    var target = statemachine.all_states.get(self.target_state_id);
    if (reportErrors and (target == null)) {
      ("Unable to find state id " + self.target_state_id).errln();
    }
    self.target_variable_name = target.variable_name;

    for (action in all_actions) {
      if (reportErrors and (action == "")) {
        (sm.name + ": Empty action in transition " + self.start + " => " + target.name).errln();
      }
    }

    if (not all_actions.isEmpty()) {
      self.actions_list = all_actions.concat(", ");
    }
  }
  
  // Transform subtransitions (choice/junction)
  for (transition in self.all_transitions) {
    var new_all_actions = new List();
    new_all_actions.addAll(all_actions);
    transition.transitionTransform(statemachine, state, trigger, new_all_actions, reportErrors);
      
    if (reportErrors and (transition.trigger != null)) {
      ("Transition starting from state " + state.name + " with trigger " + trigger + " has a trigger in a choice transition " + transition.trigger + " - this is not supported").errln();
    }
      
    if (not transition.guards.isEmpty()) {
      self.trans_conditional.add(transition);
    } else {
      self.trans_unconditional.add(transition);
    }
  }
  if (reportErrors and (self.trans_unconditional.size() > 1)) {
    ("More than one unconditional outgoing transition from state " + state.name + " for trigger " + trigger).errln();
  }
}
%]
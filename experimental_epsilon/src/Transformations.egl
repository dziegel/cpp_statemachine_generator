[%
operation Tuple transformStatemachine() {
  self.transition_type = "Transition";
  self.transition_action_type = "Transition::ActionContainer";
  self.state_type = "State";
  self.history_state_type = "HistoryState";
  self.event_type = "Event";
  self.impl_type = "Impl";
  self.impl_ptr_type = "ImplPtr";
  
  self.all_actions = new List();
  self.all_guards = new List();
  self.all_entry_exit = new List();
  
  self.region.transformRegion(self);
  
  self.all_actions = self.all_actions.sortBy(a | a);
  self.all_guards = self.all_guards.sortBy(g | g);
  self.all_entry_exit = self.all_entry_exit.sortBy(e | e);
}

operation Tuple transformRegion(statemachine : Tuple) {
  self.initial_variable_name = "k" + self.initial;

  self.states = self.states.sortBy(s | s.name);
  for (state in self.states) {
    state.transformState(statemachine, self);
  }
}

operation Tuple transformState(statemachine : Tuple, region : Tuple) {
  self.variable_name = "k" + self.name;
  self.state_type = statemachine.state_type;
  if(region.initial_history) {
    self.state_type = statemachine.history_state_type;
  }

  self.parent_variable_pointer = "nullptr";
  if (self.parent.isDefined()) {
    self.parent_variable_pointer = "&k" + self.parent;
  }
  
  self.entry_pointer = "nullptr";
  if (self.entry.isDefined()) {
    self.entry_pointer = "&Impl::" + self.entry;
    if (not statemachine.all_entry_exit.includes(self.entry)) {
      statemachine.all_entry_exit.add(self.entry);
    }
  }

  self.exit_pointer = "nullptr";
  if (self.exit.isDefined()) {
    self.exit_pointer = "&Impl::" + self.exit;
    if (not statemachine.all_entry_exit.includes(self.exit)) {
      statemachine.all_entry_exit.add(self.exit);
    }
  }

  self.initial_variable_pointer = "nullptr";
  if (self.region.isDefined()) {
    self.region.transformRegion(statemachine);
    self.initial_variable_pointer = "&" + self.region.initial_variable_name;
  }

  for (transition in self.transitions) {
    transition.transformTransition(statemachine);
  }
  self.transitions = self.transitions.sortBy(t | t.trigger + "-" + t.priority + "-" + t.start + t.target);

  self.all_events = new List();  
  for (transition in self.transitions) {
    if (not self.all_events.includes(transition.trigger)) {
      self.all_events.add(transition.trigger);
    }
  }
  self.all_events = self.all_events.sortBy(e | e);

  self.all_event_transitions = new Map();
  for (event in self.all_events) {
    self.all_event_transitions.put(event, self.transitions.select(t | t.trigger = event));
  }
}

operation Tuple transformTransition(statemachine : Tuple) {
  if (self.internal) {
    self.target_variable_name = "kNone";
  } else {
    self.target_variable_name = "k" + self.target;
  }
  
  var actions = new List();
  for (action in self.actions) {
    actions.add("&" + statemachine.impl_type + "::" + action);
    if (not statemachine.all_actions.includes(action)) {
      statemachine.all_actions.add(action);
    }
  }
  self.actions_list = actions.concat(", ");

  var guards = new List();
  for (guard in self.guards) {
    guards.add("impl->" + guard + "(event)");
    if (not statemachine.all_guards.includes(guard)) {
      statemachine.all_guards.add(guard);
    }
  }
  self.has_guard = not guards.isEmpty();
  if (self.has_guard) {
    self.priority = 0;
    self.guards_list = guards.concat(" && ");
  } else {
    self.priority = 9999999;
    self.guards_list = "true";
  }
}
%]